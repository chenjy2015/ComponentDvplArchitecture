
if (isNeedClientA.toBoolean()){
    apply plugin: 'com.android.library'
}else {
    apply plugin: 'com.android.application'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
        arg("AROUTER_GENERATE_DOC", "enable")
    }
}

//1. 定义 config 变量
def config = rootProject.ext

android {
    compileSdkVersion  config.android.compileSdkVersion
    buildToolsVersion config.android.buildToolsVersion

    defaultConfig {
        if (!isNeedClientA.toBoolean()){
            applicationId "com.example.clienta"
        }
        minSdkVersion config.android.minSdkVersion
        targetSdkVersion config.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main{
            if (isNeedClientA.toBoolean()){
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }else {
                manifest.srcFile  'src/main/module/AndroidManifest.xml'
            }
        }
    }

    dataBinding{
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation config.android.appcompat
    testImplementation config.android.junit
    implementation config.android.constraintlayout
    androidTestImplementation config.android.test_runner
    androidTestImplementation config.android.test_espresso
    //kotlin
    implementation config.android.kotlin_stdlib_jdk7
    //lib
    implementation project(':baselib')
    //kotlin arouter 注意！arouter-compiler 这个依赖必须在build.gradle中声明，只在base_module中声明然后引用base_module中引用是无效的！
    kapt config.dependencies.arouterCompiler
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

